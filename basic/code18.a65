
	STX FACMOH
	LDX FACMO+1
	STX FACMO
	LDX FACOV
	STX FACLO
	STY FACOV
	ADC #$08
ADDPR2	=ADDPRC+ADDPRC
ADDPR4	=ADDPR2+ADDPR2
ADDPR8	=ADDPR4+ADDPR4
	CMP #$18+ADDPR8
	BNE NORM3
ZEROFC	LDA #0
ZEROF1	STA FACEXP
ZEROML	STA FACSGN
	RTS
FADD2	ADC OLDOV
	STA FACOV
	LDA FACLO
	ADC ARGLO
	STA FACLO
	LDA FACMO
	ADC ARGMO
	STA FACMO
	LDA FACMOH
	ADC ARGMOH
	STA FACMOH
	LDA FACHO
	ADC ARGHO
	STA FACHO
	JMP SQUEEZ
NORM2	ADC #1
	ASL FACOV
	ROL FACLO
	ROL FACMO
	ROL FACMOH
	ROL FACHO
NORM1	BPL NORM2
	SEC
	SBC FACEXP
	BCS ZEROFC
	EOR #$FF
	ADC #1
	STA FACEXP
SQUEEZ	BCC RNDRTS
RNDSHF	INC FACEXP
	BEQ OVERR
	ROR FACHO
	ROR FACMOH
	ROR FACMO
	ROR FACLO
	ROR FACOV
RNDRTS	RTS
NEGFAC	LDA FACSGN
	EOR #$FF
	STA FACSGN
NEGFCH	LDA FACHO
	EOR #$FF
	STA FACHO
	LDA FACMOH
	EOR #$FF
	STA FACMOH
	LDA FACMO
	EOR #$FF
	STA FACMO
	LDA FACLO
	EOR #$FF
	STA FACLO
	LDA FACOV
	EOR #$FF
	STA FACOV
	INC FACOV
	BNE INCFRT
INCFAC	INC FACLO
	BNE INCFRT
	INC FACMO
	BNE INCFRT
	INC FACMOH
	BNE INCFRT
	INC FACHO
INCFRT	RTS
OVERR	LDX #ERROV
	JMP ERROR
MULSHF	LDX #RESHO-1
SHFTR2	LDY 3+ADDPRC,X
	STY FACOV
	LDY 3,X
	STY 4,X
	LDY 2,X
	STY 3,X
	LDY 1,X
	STY 2,X
	LDY BITS
	STY 1,X
SHIFTR	ADC #$08
	BMI SHFTR2
	BEQ SHFTR2
	SBC #$08
	TAY
	LDA FACOV
	BCS SHFTRT
SHFTR3	ASL 1,X
	BCC SHFTR4
	INC 1,X
SHFTR4	ROR 1,X
	ROR 1,X
ROLSHF	ROR 2,X
	ROR 3,X
	ROR 4,X
	ROR
	INY
	BNE SHFTR3
SHFTRT	CLC
	RTS
FONE	.BYTE $81,$00,$00,$00,$00
LOGCN2	.BYTE $03,$7F,$5E,$56
	.BYTE $CB,$79,$80,$13
	.BYTE $9B,$0B,$64,$80
	.BYTE $76,$38,$93,$16
	.BYTE $82,$38,$AA,$3B,$20
SQR05	.BYTE $80,$35,$04,$F3,$34
SQR20	.BYTE $81,$35,$04,$F3,$34
NEGHLF	.BYTE $80,$80,$00,$00,$00
LOG2	.BYTE $80,$31,$72,$17,$F8

