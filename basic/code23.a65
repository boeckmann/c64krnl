
	LDA #'-'
FOUT14	STA FBUFFR+1,Y
	LDA #'E'
	STA FBUFFR,Y
	TXA
	LDX #$2F
	SEC
FOUT15	INX
	SBC #10
	BCS FOUT15
	ADC #$3A
	STA FBUFFR+3,Y
	TXA
	STA FBUFFR+2,Y
	LDA #0
	STA FBUFFR+4,Y
	BEQ FOUT20
FOUT19	STA FBUFFR-1,Y
FOUT17	LDA #0
	STA FBUFFR,Y
FOUT20	LDA #<FBUFFR
	LDY #>FBUFFR
	RTS
FHALF	.BYTE $80,$00
ZERO	.BYTE $00,$00,$00
FOUTBL	.BYTE $FA,$0A,$1F,$00,$00
	.BYTE $98,$96,$80,$FF
	.BYTE $F0,$BD,$C0,$00
	.BYTE $01,$86,$A0,$FF
	.BYTE $FF,$D8,$F0,$00,$00
	.BYTE $03,$E8,$FF,$FF
	.BYTE $FF,$9C,$00,$00,$00,$0A
	.BYTE $FF,$FF,$FF,$FF
FDCEND	.BYTE $FF,$DF,$0A,$80
	.BYTE $00,$03,$4B,$C0,$FF
	.BYTE $FF,$73,$60,$00,$00
	.BYTE $0E,$10,$FF,$FF
	.BYTE $FD,$A8,$00,$00,$00,$3C
TIMEND
;
CKSMA0	.BYTE $00        ;$A000 8K ROM CHECK SUM ADJ
PATCHS	.fill 30,$AA     ; PATCH AREA
;
SQR	JSR MOVAF
	LDA #<FHALF
	LDY #>FHALF
	JSR MOVFM
FPWRT	BEQ EXP
	LDA ARGEXP
	BNE FPWRT1
	JMP ZEROF1
FPWRT1	LDX #<TEMPF3
	LDY #>TEMPF3
	JSR MOVMF
	LDA ARGSGN
	BPL FPWR1
	JSR INT
	LDA #<TEMPF3
	LDY #>TEMPF3
	JSR FCOMP
	BNE FPWR1
	TYA
	LDY INTEGR
FPWR1	JSR MOVFA1
	TYA
	PHA
	JSR LOG
	LDA #<TEMPF3
	LDY #>TEMPF3
	JSR FMULT
	JSR EXP
	PLA
	LSR
	BCC NEGRTS
NEGOP	LDA FACEXP
	BEQ NEGRTS
	LDA FACSGN
	EOR #$FF
	STA FACSGN
NEGRTS	RTS

