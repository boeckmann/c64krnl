
.if @ != $C000
	.echo "code25.a65: PC MUST be $C000, is ", [$]@
.endif

	.org $E000         ;START OF VIC-40 KERNAL ROM

; CONTINUATION OF EXPONENT ROUTINE
;
STOLD	STA OLDOV
	JSR MOVEF
	LDA FACEXP
	CMP #$88
	BCC EXP1
GOMLDV	JSR MLDVEX
EXP1	JSR INT
	LDA INTEGR
	CLC
	ADC #$81
	BEQ GOMLDV
	SEC
	SBC #1
	PHA
	LDX #4+ADDPRC
SWAPLP	LDA ARGEXP,X
	LDY FACEXP,X
	STA FACEXP,X
	STY ARGEXP,X
	DEX
	BPL SWAPLP
	LDA OLDOV
	STA FACOV
	JSR FSUBT
	JSR NEGOP
	LDA #<EXPCON
	LDY #>EXPCON
	JSR POLY
	LDA #0
	STA ARISGN
	PLA
	JSR MLDEXP
	RTS
POLYX	STA POLYPT
	STY POLYPT+1
	JSR MOV1F
	LDA #TEMPF1
	JSR FMULT
	JSR POLY1
	LDA #<TEMPF1
	LDY #>TEMPF1
	JMP FMULT
POLY	STA POLYPT
	STY POLYPT+1
POLY1	JSR MOV2F
	LDA (POLYPT),Y
	STA DEGREE
	LDY POLYPT
	INY
	TYA
	BNE POLY3
	INC POLYPT+1
POLY3	STA POLYPT
	LDY POLYPT+1
POLY2	JSR FMULT
	LDA POLYPT
	LDY POLYPT+1
	CLC
	ADC #4+ADDPRC
	BCC POLY4
	INY
POLY4	STA POLYPT
	STY POLYPT+1
	JSR FADD
	LDA #<TEMPF2
	LDY #>TEMPF2
	DEC DEGREE
	BNE POLY2
	RTS
RMULC	.BYTE $98,$35,$44,$7A,$00
RADDC	.BYTE $68,$28,$B1,$46,$00
RND	JSR SIGN
	BMI RND1
	BNE QSETNR
	JSR RDBAS
	STX INDEX1
	STY INDEX1+1
	LDY #4
	LDA (INDEX1),Y
	STA FACHO
	INY
	LDA (INDEX1),Y
	STA FACMO
	LDY #8
	LDA (INDEX1),Y
	STA FACMOH
	INY
	LDA (INDEX1),Y
	STA FACLO
	JMP STRNEX
QSETNR	LDA #<RNDX
	LDY #>RNDX
	JSR MOVFM
	LDA #<RMULC
	LDY #>RMULC
	JSR FMULT
	LDA #<RADDC
	LDY #>RADDC
	JSR FADD
RND1	LDX FACLO
	LDA FACHO
	STA FACLO
	STX FACHO
	LDX FACMOH
	LDA FACMO
	STA FACMOH
	STX FACMO
STRNEX	LDA #0
	STA FACSGN
	LDA FACEXP
	STA FACOV
	LDA #$80
	STA FACEXP
	JSR NORMAL
	LDX #<RNDX
	LDY #>RNDX
GMOVMF	JMP MOVMF

